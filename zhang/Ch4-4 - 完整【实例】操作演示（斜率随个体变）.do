

/*------张华节---手把手教你Stata软件操作与案例分析---应用面板数据模型---------*/


/*-------------------------------------Ch4-4--变系数模型--完整【实例】操作演示1--------------------------------*/



/*--【注意】-----接下来，仅考虑系数是否【随个体变】为例，进行展示------
  ---------------其他情形，如随时点、区域等变化类似处理即可，见其他do文件命令--
  ---------------差别主要在于，与变量相乘的是i.provi，LR检验的自由度发生变化--
  ---------------说法上有些变化-----------------*/


/*--【No.1】------------------------【固定变系数】---随个体i变-----LR检验----------------------*/


cd G:\Stataex                          /*----指定默认路径----*/
use G:\Stataex\panelex2ex.dta,clear      /*----打开指定路径下的数据文件----*/


encode province, gen(provi)    /*----生成新变量provi(取值为1,2,3...)替代province----*/
                               /*----注意：若个体变量为1,2,3……整数，就无需使用encode命令----*/
xtset provi year               /*----告诉Stata该数据为面板数据----*/
	
	
gen lnq=log(q)
g lnarea=log(area)
g lnfarmm=log(farmm)
g lnagchf=log(agchf)






/*---------pool回归--【约束模型】表示：H0成立时对应的回归模型------------*/

 reg lnq lnarea lnfarmm lnagchf   
/*----pool回归--quietly表示不显示回归结果---*/
estimates store POOL1

gen ll_r=e(ll)
display ll_r              /*----调用pool回归的对数似然函数值----*/







/*---第1步----【个体时点】固定效应变截距--【所有变量】系数随【个体】变化--
            --【无约束模型】表示：H1对应的回归模型------------*/
/*--------【注意】此处的原假设H0：pool模型-------------*/

quietly xtreg lnq i.provi#c.(lnarea lnfarmm lnagchf) i.year,fe
/*----与下面两种变系数模型是等价的--约束条件个数相同----
  ----仅【无约束模型】的写法不同----
xtreg lnq lnarea lnfarmm lnagchf i.provi#c.lnarea i.provi#c.lnfarmm ///
                                 i.provi#c.lnagchf i.year,fe
xtreg lnq i.provi#c.lnarea i.provi#c.lnfarmm i.provi#c.lnagchf i.year,fe
----------------------------------------------------------*/
estimates store FERERC1

gen ll_u1=e(ll)
display ll_u1             /*----调用对数似然函数值----*/

gen N1=e(N_g)
di N1                     /*----调用个体数N----*/

gen T1=e(N)/e(N_g)
di T1                     /*----调用时间长度T----*/


/*----根据LR检验定义构造LR检验统计量----*/
di "LR = " -2*(ll_r-ll_u1)                        
di invchi2tail(N1-1+T1-1+(N1-1)*3,0.05)      
/*----计算卡方检验5%显著性水平对应的临界值
     【注意】此处，连同截距项一起检验了----*/

	 
/*--【注意】--此步，若不拒绝H0，建立pooled模型即可--【检验结束】--
    --若拒绝H0，则继续下面的检验（第2步检验）----*/

	 
	 
	 
/*---第2步----【所有变量】系数随【个体】变化----------
            --【约束模型】表示：H0对应的回归模型------------*/
/*--【注意】此处的H1：【个体时点】固定效应变截距--【所有变量】系数随【个体】变化对应的模型-----------*/

quietly reg lnq i.provi#c.(lnarea lnfarmm lnagchf) 
/*----与下面两种变系数模型是等价的--约束条件个数相同----
  ----仅【约束模型】的写法不同----
reg lnq lnarea lnfarmm lnagchf i.provi#c.lnarea i.provi#c.lnfarmm i.provi#c.lnagchf 
reg lnq i.provi#c.lnarea i.provi#c.lnfarmm i.provi#c.lnagchf
----------------------------------------------------------*/
estimates store RC2

gen ll_r2=e(ll)
display ll_r2             /*----调用对数似然函数值----*/


di "LR = " -2*(ll_r2-ll_u1)                        /*----根据LR检验定义构造LR检验统计量----*/
di invchi2tail(N1-1+T1-1,0.05)      
/*----计算卡方检验5%显著性水平对应的临界值
   --【注意】此处，在所有变量系数随个体变化的前提下，
	--检验截距项是否变化----*/

	 
/*--【注意】--此步，若不拒绝H0，需进行第3-2步检验（检验所有系数是否变化）--
    --若拒绝H0，则继续下面的检验（第3-1步检验，检验截距项是否变化）----*/


	
	
	
/*---第3-1步---【个体时点】固定效应变截距模型----------
             --【约束模型】表示：H0对应的回归模型------------*/
/*--【注意】此处的H1：【个体时点】固定效应变截距--【所有变量】系数随【个体】变化对应的模型-----------*/

quietly xtreg lnq lnarea lnfarmm lnagchf i.year, fe 
estimates store FEFE31

gen ll_r31=e(ll)
display ll_r31             /*----调用对数似然函数值----*/


di "LR = " -2*(ll_r31-ll_u1)                        /*----根据LR检验定义构造LR检验统计量----*/
di invchi2tail(3*(N1-1),0.05)      
/*----计算卡方检验5%显著性水平对应的临界值
   --【注意】此处，在个体时点固定效应的前提下，
	--检验所有变量斜率是否随个体变化----*/

	 
/*--【注意】--此步，若不拒绝H0，需进行变截距检验（检验截距项随谁变化，即Ch3-4视频操作内容）--
    --若拒绝H0，则继续下面的检验（第3-1-1步检验，
	即在斜率变化的基础上，检验截距项如何变化，类似于Ch3-4视频操作内容，）----*/

	
	
     /*---第3-1-1步--【所有变量】系数随【个体】变化对应的模型----------
                   --【约束模型】表示：H0对应的回归模型------------*/
     /*--【注意】此处的H1：【个体时点】固定效应变截距--【所有变量】系数随【个体】变化对应的模型-----------*/
	 
	 *此步为第2步检验，即结合第2步检验结果判断即可。
	 
	     di "LR = " -2*(ll_r2-ll_u1)                        /*----根据LR检验定义构造LR检验统计量----*/
         di invchi2tail(N1-1+T1-1,0.05)      
         /*----计算卡方检验5%显著性水平对应的临界值
            --【注意】此处，在所有变量系数随个体变化的前提下，
	         --检验截距项是否变化----*/

	 
         /*--【注意】--此步，若不拒绝H0，建立截距项不变、斜率随个体变的模型--【检验到此结束】--
            --？？--但，注意：此情形很难解释的通，即截距项不变，斜率又变？？--
            --若拒绝H0，则继续下面的检验（第3-1-2步检验）----*/

	
	
	
     /*---第3-1-2步--【个体】固定效应--【所有变量】系数随【个体】变化对应的模型----------
                   --【约束模型】表示：H0对应的回归模型------------*/
     /*--【注意】此处的H1：【个体时点】固定效应变截距--【所有变量】系数随【个体】变化对应的模型-----------*/
         quietly xtreg lnq i.provi#c.(lnarea lnfarmm lnagchf),fe 
         /*----与下面两种变系数模型是等价的--约束条件个数相同----
           ----仅【约束模型】的写法不同----
          xtreg lnq lnarea lnfarmm lnagchf i.provi#c.lnarea i.provi#c.lnfarmm i.provi#c.lnagchf ,fe
          xtreg lnq i.provi#c.lnarea i.provi#c.lnfarmm i.provi#c.lnagchf,fe
          ----------------------------------------------------------*/
         estimates store FERC312

         gen ll_r312=e(ll)
         display ll_r312             /*----调用对数似然函数值----*/


         di "LR = " -2*(ll_r312-ll_u1)                        /*----根据LR检验定义构造LR检验统计量----*/
         di invchi2tail(T1-1,0.05)      
         /*----计算卡方检验5%显著性水平对应的临界值
           --【注意】此处，在个体固定效应、所有变量系数随个体变化的前提下，
	       --检验截距项是否随时点变化----*/

	 
         /*--【注意】--此步，若不拒绝H0，需进行第3-1-4步检验（检验截距项是否为随机效应）--
           --若拒绝H0，则继续下面的检验（第3-1-3步检验）----*/





	
         /*---第3-1-3步--【时点】固定效应--【所有变量】系数随【个体】变化对应的模型----------
                       --【约束模型】表示：H0对应的回归模型------------*/
         /*--【注意】此处的H1：【个体时点】固定效应变截距--【所有变量】系数随【个体】变化对应的模型-----------*/
         quietly reg lnq i.provi#c.(lnarea lnfarmm lnagchf) i.year 
         /*----与下面两种变系数模型是等价的--约束条件个数相同----
           ----仅【约束模型】的写法不同----
          reg lnq lnarea lnfarmm lnagchf i.provi#c.lnarea i.provi#c.lnfarmm i.provi#c.lnagchf i.year
          reg lnq i.provi#c.lnarea i.provi#c.lnfarmm i.provi#c.lnagchf i.year
          ----------------------------------------------------------*/
         estimates store FERC313

         gen ll_r313=e(ll)
         display ll_r313             /*----调用对数似然函数值----*/


         di "LR = " -2*(ll_r313-ll_u1)                        /*----根据LR检验定义构造LR检验统计量----*/
         di invchi2tail(N1-1,0.05)      
         /*----计算卡方检验5%显著性水平对应的临界值
           --【注意】此处，在个体固定效应、所有变量系数随个体变化的前提下，
	       --检验截距项是否随个体变化----*/

	 
         /*--【注意】--此步，若不拒绝H0，建立时点固定效应、所有变量系数随个体变的模型--
		 --？？--注意：截距随t变，斜率随i变--如何解释？？？------【检验到此结束】--
           --若拒绝H0，建立个体时点固定效应、所有变量系数随个体变----*/


/*--【注意】我们的例子的检验表明应该建立个体时点固定效应变截距、
     所有变量系数随个体变的模型--
	 建立下列模型进行回归分析即可---*/
xtreg lnq i.provi#c.(lnarea lnfarmm lnagchf) i.year,fe vce(cluster provi)
	 
		   
		   
		   
		   
	
	
         /*---第3-1-4步--Hausman检验：在【所有变量】系数随【个体】变化对应的基础上----------
		    --检验个体是否为随机效应--
            --H0：个体随机效应--【所有变量】系数随【个体】变化对应的模型-------------
            --H1：【个体】固定效应变截距--【所有变量】系数随【个体】变化对应的模型-----------*/

         quietly xtreg lnq i.provi#c.(lnarea lnfarmm lnagchf),fe     /*----个体固定效应回归，附Chow检验----*/
         esti store FERC314

         quietly xtreg lnq i.provi#c.(lnarea lnfarmm lnagchf),re     /*----个体随机效应变截距模型FGLS----*/
         esti store RERC314

         hausman FERC314 RERC314,constant sigmamore

         /*--【注意】--此步，若不拒绝H0，建立个体随机效应、所有变量系数随个体变的模型--
		 --？？--注意：个体随机效应--如何解释？？？------【检验到此结束】--
           --若拒绝H0，建立个体固定效应、所有变量系数随个体变----*/

	
	
	
/*---第3-2步---【所有变量】系数随【个体】变化----------
             --【无约束模型】表示：H1对应的回归模型------------*/
/*--【注意】此处的原假设H0：pooled模型-----------*/

quietly reg lnq i.provi#c.(lnarea lnfarmm lnagchf) 
/*----与下面两种变系数模型是等价的--约束条件个数相同----
  ----仅【无约束模型】的写法不同----
reg lnq lnarea lnfarmm lnagchf i.provi#c.lnarea i.provi#c.lnfarmm i.provi#c.lnagchf 
reg lnq i.provi#c.lnarea i.provi#c.lnfarmm i.provi#c.lnagchf
----------------------------------------------------------*/
estimates store RC32

gen ll_u32=e(ll)
display ll_u32             /*----调用对数似然函数值----*/


di "LR = " -2*(ll_u32-ll_r)                        /*----根据LR检验定义构造LR检验统计量----*/
di invchi2tail(3*(N1-1),0.05)      
/*----计算卡方检验5%显著性水平对应的临界值
     【注意】此处，在截距项不变的前提下，
	 检验所有变量系数是否随个体变化----*/

	 
/*--【注意】--此步，若不拒绝H0，建立pooled模型（若第1步拒绝H0，此步一般会拒绝，否则就矛盾）--
    --若拒绝H0，建立截距项不变、所有变量系数随个体变化的模型即可--【检验到此结束】--
--？？--注意：此情形很难解释的通，即截距项不变，斜率又变？？----*/

	
	
	
	
	






/*--【No.2】------------------【随机系数】--------Stata自带检验方法Swamy检验--
--【注意】当拒绝H0时，不建议采用此方法--
--理由：拒绝H0时，若模型带截距项，截距项也是随机效应，现实中很难解释的通--
--若模型不带截距项，试想一个模型无截距项，斜率变化，难以解释--------*/



xtrc lnq lnarea lnfarmm lnagchf, betas vce(boot)
/*----betas表示显示不同系数的回归结果--不包含betas默认不显示----*/

xtrc lnq lnarea lnfarmm lnagchf, betas


xtrc lnq lnarea lnfarmm lnagchf,nocon





